/*
 * This build file was auto generated by running the Gradle 'init' task
 * by 'amdonov' at '12/15/15 8:23 PM' with Gradle 2.9
 *
 * This generated file contains a commented-out sample Java project to get you started.
 * For more details take a look at the Java Quickstart chapter in the Gradle
 * user guide available at https://docs.gradle.org/2.9/userguide/tutorial_java_projects.html
 */

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'io.github.robwin:swagger2markup-gradle-plugin:0.9.1'
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.3'
    }
}

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '1.2.2'
}

apply plugin: 'io.github.robwin.swagger2markup'
apply plugin: 'org.asciidoctor.convert'

jar {
    manifest {
        attributes("Main-Class": "com.mycompany.assets.AssetServiceApplication", "Implementation-Version": version)
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

shadowJar {
    mergeServiceFiles()
}

repositories {
    jcenter()
}

dependencies {
    compile 'io.dropwizard:dropwizard-core:0.9.1'
    compile 'io.federecio:dropwizard-swagger:0.7.0'
    testCompile 'junit:junit:4.12'
    testCompile 'io.dropwizard:dropwizard-testing:0.9.1'
}

ext {
    generatedDocumentation = file('src/docs/asciidoc/generated')
}

convertSwagger2markup {
    inputDir file('src/docs/swagger')
    pathsGroupedBy io.github.robwin.swagger2markup.GroupBy.TAGS
    separatedDefinitions true
    outputDir generatedDocumentation
}

asciidoctor {
    dependsOn test
    sources {
        include 'index.adoc'
    }
    backends = ['html5']
    attributes = [
            doctype: 'book',
            toc: 'left',
            toclevels: '2',
            numbered: '',
            sectlinks: '',
            sectanchors: '',
            generated: generatedDocumentation
    ]
}

asciidoctor.dependsOn convertSwagger2markup

assemble.dependsOn shadowJar